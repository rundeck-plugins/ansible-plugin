plugins {
    id 'groovy'
}

repositories {
    mavenLocal()
    mavenCentral()
}


group = 'com.github.rundeck-plugins'

configurations {
    configurations {
        instrumentedClasspath {
            canBeConsumed = false
        }
    }
    
    // Security: Force resolution of vulnerable dependencies
    all {
        resolutionStrategy {
            force 'org.apache.commons:commons-compress:1.28.0'
            force 'org.jetbrains.kotlin:kotlin-stdlib:2.1.0'
        }
    }
}
dependencies {
    // Exclude JUnit 4 with EPL-1.0 license - Spock uses JUnit Platform (JUnit 5) instead
    testImplementation(libs.spock.core) {
        exclude group: 'junit', module: 'junit'
    }

    // Exclude JUnit 4 from TestContainers - it's not needed when using Spock
    testImplementation(libs.testcontainers) {
        exclude group: 'junit', module: 'junit'
    }
    testImplementation(libs.testcontainers.spock) {
        exclude group: 'junit', module: 'junit'
    }

    // Exclude JAXB with restrictive license from rd-api-client
    testImplementation(libs.rd.api.client) {
        exclude group: 'javax.xml.bind', module: 'jaxb-api'
    }

    testImplementation libs.jsch

    // Security: Explicit inclusion of secure versions to override transitive dependencies
    testImplementation libs.commons.compress
    testImplementation libs.kotlin.stdlib

}

tasks.register('functionalTest', Test) {
    useJUnitPlatform()
    systemProperty('RUNDECK_TEST_IMAGE', "rundeck/rundeck:5.1.1")
    description = "Run Ansible integration tests"
}

tasks.register('copyPluginArtifact', Copy) {
    from("$rootDir/build/libs/"){
        include '**/*.jar'
    }
    into "$projectDir/src/test/resources/docker/rundeck/plugins"
}