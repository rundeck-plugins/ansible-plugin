plugins {
  id 'pl.allegro.tech.build.axion-release' version '1.13.6'
}

group 'com.rundeck'

ext.rundeckPluginVersion = '1.2'
ext.pluginClassNames = [
  'com.rundeck.plugins.ansible.plugin.AnsibleResourceModelSourceFactory',
  'com.rundeck.plugins.ansible.plugin.AnsibleNodeExecutor',
  'com.rundeck.plugins.ansible.plugin.AnsibleFileCopier',
  'com.rundeck.plugins.ansible.plugin.AnsiblePlaybookWorkflowStep',
  'com.rundeck.plugins.ansible.plugin.AnsiblePlaybookInlineWorkflowStep',
  'com.rundeck.plugins.ansible.plugin.AnsibleModuleWorkflowStep',
  'com.rundeck.plugins.ansible.plugin.AnsiblePlaybookWorflowNodeStep',
  'com.rundeck.plugins.ansible.plugin.AnsiblePlaybookInlineWorkflowNodeStep'
].join(',')

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
sourceCompatibility = 1.8

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}

version = scmVersion.version

repositories {
  mavenCentral()
  mavenLocal()
}

task setHttpProxyFromEnv {
    def map = ['HTTP_PROXY': 'http', 'HTTPS_PROXY': 'https']
    for (e in System.getenv()) {
        def key = e.key.toUpperCase()
        if (key in map) {
            def base = map[key]
            def url = e.value.toURL()
            println " - systemProp.${base}.proxy=${url.host}:${url.port}"
            System.setProperty("${base}.proxyHost", url.host.toString())
            System.setProperty("${base}.proxyPort", url.port.toString())
        }
    }
}

configurations {
  pluginLibs

  build.dependsOn setHttpProxyFromEnv
  compile {
    extendsFrom pluginLibs
  }
}

dependencies {
  pluginLibs 'com.google.code.gson:gson:2.8.5'
  compile 'org.codehaus.groovy:groovy-all:2.5.6'
  compile 'org.rundeck:rundeck-core:3.0.1-20180803'
}

task copyToLib(type: Copy) {
  into "$buildDir/output/lib"
  from configurations.pluginLibs
}

jar {
  from "$buildDir/output"
  manifest {
    def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
    attributes 'Rundeck-Plugin-Name' : 'Ansible Integration'
    attributes 'Rundeck-Plugin-Description' : 'This plugin brings basic Ansible support to Rundeck. It imports hosts from Ansible\'s inventory, including a bunch of facts, and can run modules and playbooks. There is also a node executor and file copier for your project.'
    attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.0.1+'
    attributes 'Rundeck-Plugin-Tags': 'java,node executor,resource model,workflow step,ansible'
    attributes 'Rundeck-Plugin-License': 'MIT'
    attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/ansible-plugin'
    attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
    attributes 'Rundeck-Plugin-Author': 'David Kirstein'
    attributes 'Rundeck-Plugin-Classnames': pluginClassNames
    attributes 'Rundeck-Plugin-File-Version': version
    attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
    attributes 'Rundeck-Plugin-Archive': 'true'
    attributes 'Rundeck-Plugin-Libs': "${libList}"
    attributes 'Rundeck-Plugin-URL': 'https://github.com/rundeck-plugins/ansible-plugin'
  }
  dependsOn(copyToLib)
}
