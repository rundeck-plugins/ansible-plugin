plugins {
  id 'pl.allegro.tech.build.axion-release' version '1.17.2'
  id 'maven-publish'
}

group = 'com.github.rundeck-plugins'

ext.rundeckPluginVersion = '1.2'
ext.pluginClassNames = [
  'com.rundeck.plugins.ansible.plugin.AnsibleResourceModelSourceFactory',
  'com.rundeck.plugins.ansible.plugin.AnsibleNodeExecutor',
  'com.rundeck.plugins.ansible.plugin.AnsibleFileCopier',
  'com.rundeck.plugins.ansible.plugin.AnsiblePlaybookWorkflowStep',
  'com.rundeck.plugins.ansible.plugin.AnsiblePlaybookInlineWorkflowStep',
  'com.rundeck.plugins.ansible.plugin.AnsibleModuleWorkflowStep',
  'com.rundeck.plugins.ansible.plugin.AnsiblePlaybookWorflowNodeStep',
  'com.rundeck.plugins.ansible.plugin.AnsiblePlaybookInlineWorkflowNodeStep',
  'com.rundeck.plugins.ansible.logging.AnsibleSetStatsFilterPlugin',
  'com.rundeck.plugins.ansible.plugin.AnsiblePluginGroup'
].join(',')

apply plugin: 'java'
apply plugin: 'groovy'
sourceCompatibility = 17
targetCompatibility = 17

scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}

version = scmVersion.version
// Override version for Grails 7 upgrade
version = '4.0.14-grails7-upgrade-test'

repositories {
  mavenLocal()
    maven {
        name = 'Central Portal Snapshots'
        url = 'https://central.sonatype.com/repository/maven-snapshots/'
        content {
            includeGroup('org.rundeck')
        }
    }  // Must be FIRST for Grails 7 development
    maven {
        name = 'Central Portal Snapshots'
        url = 'https://central.sonatype.com/repository/maven-snapshots/'
        
        // Only search this repository for org.rundeck snapshots
        content {
            includeGroup('org.rundeck')
        }
    }
  mavenCentral()
}

configurations {
  pluginLibs
  
  // Security: Force resolution of vulnerable dependencies
  all {
    resolutionStrategy {
      force 'org.apache.commons:commons-lang3:3.18.0'
      force 'org.apache.commons:commons-compress:1.28.0'
      force 'org.jetbrains.kotlin:kotlin-stdlib:2.1.0'
      // Exclude old commons-lang to prevent conflicts
      exclude group: 'commons-lang', module: 'commons-lang'
    }
  }

  implementation {
    extendsFrom pluginLibs
  }
}

dependencies {
  pluginLibs libs.gson
  implementation libs.rundeck.core
  implementation libs.groovy.all
  pluginLibs libs.jackson.core
  pluginLibs libs.jackson.dataformat.yaml
  implementation libs.snakeyaml


  // Security: Explicit inclusion of secure versions
  implementation libs.commons.lang3
  implementation libs.commons.compress

  compileOnly libs.lombok
  annotationProcessor libs.lombok

  testCompileOnly libs.lombok
  testAnnotationProcessor libs.lombok

  testImplementation libs.spock.core
  testImplementation libs.cglib.nodep
  testImplementation libs.objenesis

}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
  
  // Java 17+ module access for cglib/Spock mocking
  jvmArgs = [
    '--add-opens=java.base/java.lang=ALL-UNNAMED',
    '--add-opens=java.base/java.util=ALL-UNNAMED',
    '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED',
    '--add-opens=java.base/java.io=ALL-UNNAMED',
    '--add-opens=java.base/java.net=ALL-UNNAMED'
  ]
}

task copyToLib(type: Copy) {
  into "$buildDir/output/lib"
  from configurations.pluginLibs
}

jar {
  from "$buildDir/output"
  manifest {
    def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
    attributes 'Rundeck-Plugin-Name' : 'Ansible Integration'
    attributes 'Rundeck-Plugin-Description' : 'This plugin brings basic Ansible support to Rundeck. It imports hosts from Ansible\'s inventory, including a bunch of facts, and can run modules and playbooks. There is also a node executor and file copier for your project.'
    attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.0.1+'
    attributes 'Rundeck-Plugin-Tags': 'java,node executor,resource model,workflow step,ansible'
    attributes 'Rundeck-Plugin-License': 'MIT'
    attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/ansible-plugin'
    attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
    attributes 'Rundeck-Plugin-Author': 'David Kirstein'
    attributes 'Rundeck-Plugin-Classnames': pluginClassNames
    attributes 'Rundeck-Plugin-File-Version': version
    attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
    attributes 'Rundeck-Plugin-Archive': 'true'
    attributes 'Rundeck-Plugin-Libs': "${libList}"
    attributes 'Rundeck-Plugin-URL': 'https://github.com/rundeck-plugins/ansible-plugin'
  }
  dependsOn(copyToLib)
}

publishing {
  publications {
    maven(MavenPublication) {
      artifactId = 'ansible-plugin'
      from components.java
    }
  }
}
